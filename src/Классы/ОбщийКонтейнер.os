#Использовать 1commands

Перем _Образ;
Перем _ИДКонтейнера;
Перем _ОткрытыеПорты;


Функция ОткрытьПорт(Порт) Экспорт
	_ОткрытыеПорты.Вставить(Порт, 0);

	Возврат ЭтотОбъект;
КонецФункции

Функция ПолучитьОткрытыйПорт(Порт) Экспорт
	Возврат _ОткрытыеПорты.Получить(Порт);
КонецФункции

Процедура Запустить() Экспорт
	// Запуск контейнера
	Команда = Новый Команда;
	Команда.УстановитьКоманду("docker");
	Команда.ДобавитьПараметр("run");
	Команда.ДобавитьПараметр("-d");

	Для Каждого Порт Из _ОткрытыеПорты Цикл
		Команда.ДобавитьПараметр("-p");
		Команда.ДобавитьПараметр(":" + Порт.Ключ);
	КонецЦикла;

	Команда.ДобавитьПараметр(_Образ);

	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.Исполнить();

	Вывод = Команда.ПолучитьВывод();
	СтрокиВывода = СтрРазделить(Вывод, Символы.ПС, Ложь);

	_ИДКонтейнера = СокрЛП(СтрокиВывода[СтрокиВывода.ВГраница()]);

	// Получение информации о портах
	Команда = Новый Команда;
	Команда.УстановитьКоманду("docker");
	Команда.ДобавитьПараметр("port");
	Команда.ДобавитьПараметр(_ИДКонтейнера);

	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.Исполнить();

	Вывод = Команда.ПолучитьВывод();
	СтрокиВывода = СтрРазделить(Вывод, Символы.ПС, Ложь);
	// 80/tcp -> 0.0.0.0:32768
	// 80/tcp -> [::]:32768
	// Игнорируем ipv6 пока
	Для Каждого Строка Из СтрокиВывода Цикл
		ЧастиСтроки = СтрРазделить(Строка, "->", Ложь);
		ВнутренняяЧасть = СтрРазделить(СокрЛП(ЧастиСтроки[0]), "/", Ложь);
		ВнешняяЧасть = СтрРазделить(СокрЛП(ЧастиСтроки[1]), ":", Ложь);
		Если ВнешняяЧасть.Количество() <> 2 Тогда
			// IPv6 - пропускаем
			Продолжить;
		КонецЕсли;

		ВнутреннийПорт = Число(ВнутренняяЧасть[0]);
		ВнешнийПорт = Число(ВнешняяЧасть[1]);

		_ОткрытыеПорты.Вставить(ВнутреннийПорт, ВнешнийПорт);
	КонецЦикла;

КонецПроцедуры

Процедура Остановить() Экспорт
	Команда = Новый Команда;
	Команда.УстановитьКоманду("docker");
	Команда.ДобавитьПараметр("stop");
	Команда.ДобавитьПараметр(_ИДКонтейнера);

	Команда.ПоказыватьВыводНемедленно(Истина);

	Команда.Исполнить();
КонецПроцедуры

Процедура ПриСозданииОбъекта(Образ)
	_Образ = Образ;
	_ОткрытыеПорты = Новый Соответствие;
КонецПроцедуры
